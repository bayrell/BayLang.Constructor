/*!
 *  BayLang Constructor
 */

namespace BayLang.Constructor.Frontend.Editor;

use Runtime.lib;
use Runtime.Callback;
use Runtime.Web.BaseModel;
use BayLang.Constructor.Frontend.Editor.Parameter;
use BayLang.Constructor.Frontend.Editor.SelectedItem;
use BayLang.Constructor.Frontend.Editor.Widget;


class SelectedItemModel extends BaseModel
{
	string component = classof SelectedItem;
	string widget_name = "selected";
	
	/* Selected item */
	Collection<int> path = null;
	Widget widget = null;
	
	
	/**
	 * Select item
	 */
	void selectItem(Collection<int> path)
	{
		/* Change path */
		this.path = path ? path.slice() : null;
		
		/* Get item */
		Widget widget = this.parent_widget.component_processor.getWidget(path);
		if (widget == this.widget) return;
		
		/* Set widget */
		this.widget = widget;
		
		/* Setup widget */
		this.widget.setup();
	}
	
	
	/**
	 * Change param
	 */
	void changeParameterValue(Parameter param, var value)
	{
		/* Change attr value */
		param.setValue(value);
		
		/* Update label */
		if (param.name == "widget_name")
		{
			param.widget.item.updateLabel();
		}
		
		/* Update render */
		this.parent_widget.updateFrameRender();
	}
}