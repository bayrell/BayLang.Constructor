/*!
 *  BayLang Technology
 *
 *  (c) Copyright 2016-2024 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace BayLang.Constructor.Frontend.Editor.Styles;

use BayLang.OpCodes.BaseOpCode;
use BayLang.OpCodes.OpHtmlCSSAttribute;
use BayLang.OpCodes.OpHtmlCSS;
use Runtime.BaseObject;


class Style extends BaseObject
{
	string selector = "";
	OpHtmlCSS op_code = null;
	Map<string> values = {};
	string content = "";
	
	
	/**
	 * Copy item
	 */
	void copy()
	{
		Style item = static::newInstance();
		item.selector = this.selector;
		item.op_code = this.op_code ? rtl::copy(this.op_code) : null;
		item.values = this.values.copy();
		item.content = this.content;
		return item;
	}
	
	
	/**
	 * Set class name
	 */
	void setClassName(string class_name)
	{
		this.selector = "." ~ class_name;
	}
	
	
	/**
	 * Set selector
	 */
	void setSelector(string selector)
	{
		this.selector = selector;
	}
	
	
	/**
	 * Set op code
	 */
	void setOpCode(OpHtmlCSS op_code)
	{
		this.op_code = op_code;
		this.initValues();
		this.updateContent();
	}
	
	
	/**
	 * Init values
	 */
	void initValues()
	{
		this.values = {};
		for (int i=0; i<this.op_code.items.count(); i++)
		{
			BaseOpCode item = this.op_code.items.get(i);
			if (not (item instanceof OpHtmlCSSAttribute)) continue;
			this.values.set(item.key, {
				"op_code": item,
				"value": item.value,
			});
		}
	}
	
	
	/**
	 * Build content
	 */
	void buildContent()
	{
		Vector<string> content = this.values.transition(
			string (Map item, string key) => key ~ ": " ~ item.get("value")
		);
		return rs::join(";\n", content);
	}
	
	
	/**
	 * Update content
	 */
	void updateContent()
	{
		this.content = this.buildContent();
	}
	
	
	/**
	 * Set new content
	 */
	void setContent(string content)
	{
		this.content = content;
	}
	
	
	/**
	 * Returns style
	 */
	void getStyle(string component_name)
	{
		string component_hash = rs::getComponentHash(component_name);
		return this.selector ~ "." ~ component_hash ~ "{" ~ rs::replace("\n", "", this.content) ~ "}";
	}
}