/*!
 *  BayLang Technology
 *
 *  (c) Copyright 2016-2024 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace BayLang.Constructor.Frontend.Editor;

use Runtime.ApiResult;
use Runtime.BaseModel;
use Runtime.Widget.ContextMenu.ContextMenuMessage;
use Runtime.Widget.ContextMenu.ContextMenuModel;
use Runtime.Widget.Tree.TreeMessage;
use Runtime.Widget.Tree.TreeModel;
use BayLang.OpCodes.BaseOpCode;
use BayLang.OpCodes.OpCodeType;
use BayLang.OpCodes.OpHtmlContent;
use BayLang.OpCodes.OpHtmlTag;
use BayLang.Constructor.Frontend.Editor.Dialog.AddDialogModel;
use BayLang.Constructor.Frontend.Editor.Dialog.RemoveDialogModel;
use BayLang.Constructor.Frontend.Editor.Dialog.SaveDialogModel;
use BayLang.Constructor.Frontend.Editor.Manager.CodeManager;
use BayLang.Constructor.Frontend.Editor.Settings.WidgetDialogModel;
use BayLang.Constructor.Frontend.Editor.Styles.StyleManager;
use BayLang.Constructor.Frontend.Editor.Widget.Dnd;
use BayLang.Constructor.Frontend.Editor.Widget.Widget;
use BayLang.Constructor.Frontend.Editor.Widget.WidgetComponent;
use BayLang.Constructor.Frontend.Editor.Widget.WidgetTag;
use BayLang.Constructor.Frontend.Editor.Widget.WidgetText;
use BayLang.Constructor.Frontend.Editor.EditorPage;
use BayLang.Constructor.WidgetPage.WidgetPageModel;


class EditorPageModel extends BaseModel
{
	string component = classof EditorPage;
	string page_model = "";
	WidgetPageModel page_widget = null;
	Widget widget = null;
	Vector selected_path = null;
	Widget selected_widget = null;
	BaseOpCode code_model = new CodeManager();
	BaseOpCode code_component = new CodeManager();
	AddDialogModel add_dialog = null;
	RemoveDialogModel remove_dialog = null;
	SaveDialogModel save_dialog = null;
	ContextMenuModel contextmenu = null;
	WidgetDialogModel widget_settings = null;
	TreeModel widget_tree = null;
	StyleManager styles = new StyleManager(this);
	
	
	/**
	 * Init params
	 */
	void initParams(Map params)
	{
		parent(params);
		
		if (params.has("page_model")) this.page_model = params.get("page_model");
		if (params.has("page_widget")) this.page_widget = params.get("page_widget");
	}
	
	
	/**
	 * Init widget
	 */
	void initWidget(Map params)
	{
		parent(params);
		
		this.add_dialog = this.createWidget(classof AddDialogModel);
		this.remove_dialog = this.createWidget(classof RemoveDialogModel);
		this.save_dialog = this.createWidget(classof SaveDialogModel);
		this.widget_settings = this.createWidget(classof WidgetDialogModel, {});
		this.widget_tree = this.createWidget(classof TreeModel, {
			"autoselect": false,
			"dnd": new Dnd(),
		});
		this.widget_tree.dnd.editor = this;
		
		/* Contextmenu */
		this.contextmenu = this.createWidget(classof ContextMenuModel, {
			"items": [
				{
					"label": "Add",
					"key": "add",
				},
				{
					"label": "Remove",
					"key": "remove",
				},
				/*{
					"label": "Settings",
					"key": "settings",
				}*/
			]
		});
		this.contextmenu.addEventListener("clickItem", method this.onContextMenuClick);
		
		/* Add events */
		this.widget_tree.addEventListener("selectItem", method this.onTreeSelectItem);
	}
	
	
	/**
	 * Returns true if is loaded
	 */
	bool isLoaded() => this.widget != null;
	
	
	/**
	 * Load code
	 */
	async void loadCode()
	{
		ApiResult result = await this.layout.sendApi({
			"api_name": "baylang.constructor.widget",
			"method_name": "load",
			"data": {
				"widget_name": this.page_model,
			}
		});
		if (result.isSuccess())
		{
			this.code_model.assign(result.data.get("model"));
			this.code_component.assign(result.data.get("component"));
			if (this.code_component.isLoaded())
			{
				this.widget = new WidgetTag(this);
				this.widget.setCode(this.code_component.op_render);
				this.widget_tree.root = this.widget.tree_item;
			}
			this.styles.init();
		}
	}
	
	
	/**
	 * Save
	 */
	async void save()
	{
		this.styles.refresh();
		this.styles.updateOpCode();
		
		this.save_dialog.setStatus(SaveDialogModel::STATUS_SAVING);
		this.save_dialog.show();
		
		ApiResult result = await this.layout.sendApi({
			"api_name": "baylang.constructor.widget",
			"method_name": "save",
			"data": {
				"widget_name": this.page_model,
				"code_model": rtl::jsonEncode(rtl::serialize(this.code_model.code)),
				"code_component": rtl::jsonEncode(rtl::serialize(this.code_component.code)),
			}
		});
		this.save_dialog.setApiResult(result);
	}
	
	
	/**
	 * Select item
	 */
	void selectItem(Vector path)
	{
		this.selected_path = path;
		this.selected_widget = this.widget ? this.widget.get(path) : null;
		this.widget_tree.selectItem(path);
		if (this.selected_widget)
		{
			this.selected_widget.onSelect();
		}
		if (path == null)
		{
			this.parent_widget.selected_elem = null;
		}
	}
	
	
	/**
	 * Context menu click
	 */
	async void onContextMenuClick(ContextMenuMessage message)
	{
		this.contextmenu.hide();
		
		string key = message.item.get("key");
		if (key == "add")
		{
			this.showAddWidget();
		}
		else if (key == "remove")
		{
			this.showRemoveWidget();
		}
		else if (key == "settings")
		{
			this.showSettings();
		}
	}
	
	
	/**
	 * Tree select item
	 */
	void onTreeSelectItem(TreeMessage message)
	{
		this.selectItem(message.path);
	}
	
	
	/**
	 * Update select box
	 */
	void updateSelectedBox()
	{
		window.setTimeout(void (){ this.parent_widget.updateSelectedBox(); }, 1);
	}
	
	
	/**
	 * Show add widget
	 */
	void showAddWidget()
	{
		this.add_dialog.show();
	}
	
	
	/**
	 * Show remove widget
	 */
	void showRemoveWidget()
	{
		this.remove_dialog.show();
	}
	
	
	/**
	 * Show settings
	 */
	void showSettings()
	{
		this.widget_settings.show();
	}
}