/*!
 *  BayLang Constructor
 */

namespace BayLang.Constructor.Frontend.Editor;

use Runtime.lib;
use Runtime.Callback;
use Runtime.Web.BaseModel;
use BayLang.Constructor.Frontend.Editor.StyleItem;
use BayLang.Constructor.Frontend.Editor.Styles;
use BayLang.OpCodes.BaseOpCode;
use BayLang.OpCodes.OpDeclareClass;
use BayLang.OpCodes.OpHtmlStyle;
use BayLang.OpCodes.OpModule;
use BayLang.OpCodes.OpString;
use BayLang.OpCodes.OpUse;


class StylesModel extends BaseModel
{
	string component = classof Styles;
	string widget_name = "styles";
	Dict<StyleItem> items = {};
	OpHtmlStyle main_style_code = null;
	
	
	/**
	 * Returns CSS content
	 */
	string getCSS()
	{
		Vector items = [];
		Collection selectors = this.items.keys().sort();
		for (int i=0; i<selectors.count(); i++)
		{
			string selector_name = selectors.get(i);
			StyleItem item = this.items.get(selector_name);
			items.push(item.content);
		}
		return rs::join("\n", items);
	}
	
	
	/**
	 * Returns source
	 */
	pure string getCSSCode(string selector_name, string source)
	{
		return selector_name ~ "{" ~ source ~ "}";
	}
	
	
	/**
	 * Returns css content
	 */
	string getSelectorContent(string selector_name)
	{
		if (not this.items.has(selector_name)) return "";
		return this.items.get(selector_name).source;
	}
	
	
	/**
	 * Create style item
	 */
	StyleItem createStyleItem(string selector_name, OpHtmlStyle op_code = null)
	{
		StyleItem style_item = new StyleItem();
		style_item.op_code = op_code;
		style_item.selector_name = selector_name;
		style_item.parent_widget = this;
		this.items.set(selector_name, style_item);
		return style_item;
	}
	
	
	/**
	 * Set css content
	 */
	void setSelectorContent(string selector_name, string source, OpHtmlStyle op_code = null)
	{
		/* Default item */
		if (op_code == null)
		{
			op_code = this.main_style_code;
		}
		
		/* Create style item if does not exists */
		if (not this.items.has(selector_name))
		{
			this.createStyleItem(selector_name, op_code);
		}
		
		/* Set content */
		StyleItem style_item = this.items.get(selector_name);
		style_item.setContent(source);
	}
	
	
	/**
	 * Change css content
	 */
	void changeSelectorContent(string selector_name, string value)
	{
		/* Set CSS Value */
		this.setSelectorContent(selector_name, value);
		
		/* Update CSS */
		this.parent_widget.updateFrameCSS();
	}
	
	
	/**
	 * Clear styles
	 */
	void clearStyles()
	{
		this.items = {};
	}
	
	
	/**
	 * Setup styles
	 */
	void setupStyles(OpModule op_code)
	{
		OpDeclareClass op_code_class = op_code.findClass();
		if (not op_code_class) return;
		
		/* Add styles */
		for (int i=0; i<op_code_class.items.count(); i++)
		{
			BaseOpCode item = op_code_class.items.get(i);
			if (item instanceof OpHtmlStyle)
			{
				this.addHtmlStyle(item);
			}
		}
		
		/* Add style if not exists */
		if (this.main_style_code == null)
		{
			this.createMainHtmlStyle(op_code);
		}
	}
	
	
	/**
	 * Create main HTML Style
	 */
	void createMainHtmlStyle(OpModule op_code)
	{
		if (this.main_style_code != null) return;
		
		OpDeclareClass op_code_class = op_code.findClass();
		if (not op_code_class) return;
		
		/* Create main style code */
		this.main_style_code = new OpHtmlStyle
		{
			"value": new OpString
			{
				"value": ""
			}
		};
		
		/* Find use */
		int pos = -1;
		for (int i=0; i<op_code.items.count(); i++)
		{
			if (op_code.items.get(i) instanceof OpUse) pos = i;
		}
		
		/* Register main style */
		op_code_class.items.add(this.main_style_code, pos);
	}
	
	
	/**
	 * Add HTML style
	 */
	void addHtmlStyle(OpHtmlStyle op_code)
	{
		if (this.main_style_code == null)
		{
			this.main_style_code = op_code;
		}
		
		Dict styles = op_code.readStyles();
		Collection selectors = styles.keys();
		for (int i=0; i<selectors.count(); i++)
		{
			string selector_name = selectors.get(i);
			if (this.items.has(selector_name)) continue;
			
			/* Set CSS Value */
			string source = styles.get(selector_name);
			this.setSelectorContent(selector_name, source, op_code);
		}
	}
	
	
	/**
	 * Update HTML Style
	 */
	void updateHtmlStyle(OpHtmlStyle op_code)
	{
		/* Build style source */
		Vector source = [];
		Collection selectors = this.items.keys().sort();
		for (int i=0; i<selectors.count(); i++)
		{
			string selector_name = selectors.get(i);
			StyleItem style_item = this.items.get(selector_name);
			if (style_item.op_code != op_code) continue;
			
			/* Add content to source */
			source.push(selector_name ~ "{");
			Collection lines = rs::split("\n", style_item.source);
			lines = lines.map(string (string s) => "\t" ~ s);
			source.appendItems(lines);
			source.push("}");
		}
		
		/* Set content to html style op_code */
		string content = rs::join("\n", source);
		op_code.content = content;
		if (op_code.value instanceof OpString)
		{
			op_code.value.value = content;
		}
	}
}