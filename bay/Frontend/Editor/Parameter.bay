/*!
 *  BayLang Constructor
 */

namespace BayLang.Constructor.Frontend.Editor;

use Runtime.BaseObject;
use Runtime.Web.Annotations.Param;
use BayLang.Constructor.Frontend.Editor.OpCodeModel;
use BayLang.Constructor.Frontend.Editor.WidgetTreeItem;
use BayLang.OpCodes.OpHtmlAttribute;


class Parameter extends BaseObject
{
	string label = "";
	string name = "";
	var value = null;
	bool display = false;
	OpHtmlAttribute op_attr = null;
	Param param = null;
	
	/* Selected item */
	Collection<int> path = null;
	WidgetTreeItem item = null;
	
	
	/**
	 * Constructor
	 */
	public void constructor(Dict params = null)
	{
		parent();
		this._assign_values(params);
	}
	
	
	/**
	 * Set attribute
	 */
	void setHtmlAttribute(OpHtmlAttribute op_attr)
	{
		this.op_attr = op_attr;
		this.value = OpCodeModel::getValueFromOpCode(op_attr.value);
		
		if (op_attr.key == "class")
		{
			this.value = rs::split(" ", this.value);
		}
	}
	
	
	/**
	 * Set value
	 */
	void setValue(var value)
	{
		/* Set value */
		this.value = value;
		
		/* Create html attribute */
		if (this.op_attr == null)
		{
			this.op_attr = new OpHtmlAttribute
			{
				"key": this.name,
			};
			this.item.op_code.attrs.prepend(this.op_attr);
		}
		
		/* Change class name value */
		if (this.name == "class_name")
		{
			value = rs::join(" ", value);
		}
		
		/* Set html attribute value */
		this.op_attr.value = OpCodeModel::getOpCodeByValue(value);
	}
}