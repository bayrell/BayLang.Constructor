/*!
 *  BayLang Constructor
 */

namespace BayLang.Constructor.Frontend.Editor.Dialog;

use Runtime.lib;
use Runtime.Callback;
use Runtime.Widget.ButtonModel;
use Runtime.Widget.Dialog.ConfirmDialogModel;
use BayLang.Constructor.Frontend.Annotations.WidgetSettingsInterface;
use BayLang.Constructor.Frontend.Editor.Dialog.AddItemDialog;
use BayLang.Constructor.Frontend.Editor.CodeModel;
use BayLang.Constructor.Frontend.Editor.Widget;
use BayLang.Constructor.WidgetPage.EditorProvider;
use BayLang.OpCodes.OpHtmlAttribute;
use BayLang.OpCodes.OpHtmlTag;


class AddItemDialogModel extends ConfirmDialogModel
{
	string component = classof AddItemDialog;
	string widget_name = "add_item_dialog";
	string kind = "after";
	int step = 1;
	Collection<WidgetSettingsInterface> current_widgets = null;
	
	/* Selected item */
	Widget selected_widget = null;
	Collection<int> selected_widget_path = null;
	Dict selected_widget_info = null;
	
	
	/**
	 * Init widget settings
	 */
	void initWidget(Dict params)
	{
		parent(params);
	}
	
	
	/**
	 * Select item
	 */
	void selectItem(string widget_name)
	{
		string default_widget_name = "widget";
		
		/* Default template */
		EditorProvider editor = this.parent_widget.getFrameEditor();
		WidgetSettingsInterface widget_settings = editor.getWidgetSettings(widget_name);
		if (widget_settings)
		{
			default_widget_name = widget_settings.getSelectorName();
		}
		
		/* Create widget name */
		string widget_name_class = widget_name;
		string widget_name_value = this.parent_widget.attribute_processor
			.createWidgetName(default_widget_name);
		
		/* Create widget */
		OpHtmlTag op_code = this.parent_widget.component_processor.createWidget(
			widget_name_class, widget_name_value
		);
		this.parent_widget.addOpCode(op_code, this.selected_widget_path, this.kind);
		this.hide();
	}
	
	
	/**
	 * Returns widget label
	 */
	string getTagWidgetInfo(OpHtmlTag op_code)
	{
		OpHtmlAttribute class_name_attr = op_code.attrs.findItem(
			lib::equalAttr("key", "class")
		);
		if (not class_name_attr)
		{
			return
			{
				"label": op_code.tag_name,
				"tag_name": op_code.tag_name,
				"class_name": "",
				"widget_name": "",
			};
		};
		
		string class_name = class_name_attr.value.value;
		Collection attrs = rs::split(" ", class_name);
		attrs = attrs.filter(lib::equalNot(""));
		return
		{
			"label": op_code.tag_name ~ "." ~ attrs.first(),
			"tag_name": op_code.tag_name,
			"class_name": class_name,
			"widget_name": attrs.first(),
		};
	}
	
	
	/**
	 * Returns frame widgets
	 */
	Collection<WidgetSettingsInterface> getFrameWidgets()
	{
		EditorProvider editor = this.parent_widget.getFrameEditor();
		Collection<WidgetSettingsInterface> widgets = editor.getWidgets();
		widgets = widgets.filter(
			bool (WidgetSettingsInterface widget)
			{
				string name = widget.getComponentName();
				if (name == null) return false;
				if (rs::substr(name, -5) == "Model") return false;
				if (not this.selected_widget.canInsert(widget)) return false;
				return true;
			}
		);
		return widgets;
	}
	
	
	/**
	 * Show dialog
	 */
	void show(Collection<string> selected_path = null, string kind = "after")
	{
		this.step = 1;
		this.kind = kind;
		this.title = "Add item";
		
		/* Setup selected widget */
		this.selected_widget_path = selected_path ? selected_path.slice() : null;
		if (this.selected_widget_path)
		{
			this.selected_widget = this.parent_widget.component_processor
				.getWidget(this.selected_widget_path)
			;
		}
		else
		{
			this.selected_widget = this.parent_widget.component_processor.main_widget;
		}
		
		/* Get widgets list */
		this.current_widgets = this.getFrameWidgets();
		parent();
	}
}