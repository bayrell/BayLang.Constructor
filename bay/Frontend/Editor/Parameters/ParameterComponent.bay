/*!
 *  BayLang Constructor
 */

namespace BayLang.Constructor.Frontend.Editor.Parameters;

use Runtime.lib;
use BayLang.Constructor.Frontend.Editor.Parameters.Parameter;
use BayLang.Constructor.Frontend.Editor.Processor.CodeProcessor;
use BayLang.Constructor.Frontend.Editor.Widget;
use BayLang.OpCodes.OpHtmlAttribute;


class ParameterComponent extends Parameter
{
	Dict props = null;
	OpHtmlAttribute op_code = null;
	
    
	/**
	 * Is op_code
	 */
	void isOpCode(OpHtmlAttribute op_code)
	{
		return op_code instanceof OpHtmlAttribute and this.name == op_code.key;
	}
	
	
	/**
	 * Set op_code
	 */
	void setOpCode(OpHtmlAttribute op_code)
	{
		this.op_code = op_code;
		this.value = CodeProcessor::getValueFromOpCode(op_code.value);
	}
	
	
	/**
	 * Create attribute
	 */
	void createHtmlAttribute()
	{
		if (this.op_code != null) return;
		this.op_code = new OpHtmlAttribute
		{
			"key": this.name,
		};
		this.widget.item.code.attrs.prepend(this.op_code);
	}
	
	
	/**
	 * Remove attribute
	 */
	void removeHtmlAttribute()
	{
		if (this.op_code == null) return;
		int pos = this.widget.item.code.attrs.findItem(lib::equalAttr("key", this.name));
		this.widget.item.code.attrs.remove(pos);
		this.op_code = null;
	}
	
	
	/**
	 * Set value
	 */
	void setValue(var value)
	{
		if (value == "")
		{
			this.removeHtmlAttribute();
			this.value = "";
			return;
		}
		
		/* Create html attribute */
		if (this.op_code == null)
		{
			this.createHtmlAttribute();
		}
		
		/* Set value */
		this.value = value;
		this.op_code.value = CodeProcessor::getOpCodeByValue(value);
	}
}