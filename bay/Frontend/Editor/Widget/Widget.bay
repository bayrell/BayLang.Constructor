/*!
 *  BayLang Technology
 *
 *  (c) Copyright 2016-2024 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace BayLang.Constructor.Frontend.Editor.Widget;

use Runtime.BaseObject;
use Runtime.VirtualDom;
use BayLang.OpCodes.BaseOpCode;
use BayLang.OpCodes.OpHtmlTag;
use BayLang.OpCodes.OpHtmlContent;
use BayLang.Constructor.Frontend.Editor.Manager.CodeStrategyInterface;
use BayLang.Constructor.Frontend.Editor.Manager.OpCodeStrategy;
use BayLang.Constructor.Frontend.Editor.Manager.VirtualDomStrategy;
use BayLang.Constructor.Frontend.Editor.Parameters.Parameter;
use BayLang.Constructor.Frontend.Editor.Widget.TreeItem;
use BayLang.Constructor.Frontend.Editor.Widget.WidgetComponent;
use BayLang.Constructor.Frontend.Editor.Widget.WidgetTag;
use BayLang.Constructor.Frontend.Editor.Widget.WidgetText;
use BayLang.Constructor.Frontend.Editor.EditorPageModel;


class Widget extends BaseObject
{
	var op_code = null;
	int id = 0;
	CodeStrategyInterface code_strategy = null;
	Widget parent_widget = null;
	EditorPageModel page_model = null;
	Vector<Widget> items = [];
	Vector<Parameter> params = null;
	TreeItem tree_item = null;
	
	
	/**
	 * Returns widget name by op code
	 */
	static string getWidgetName(var op_code)
	{
		if (op_code instanceof OpHtmlTag)
		{
			if (op_code.is_component) return classof WidgetComponent;
			
			BaseOpCode first_item = op_code.content.get(0);
			if (op_code.content.count() == 1 and first_item instanceof OpHtmlContent)
			{
				return classof WidgetText;
			}
			return classof WidgetTag;
		}
		else if (op_code instanceof VirtualDom)
		{
			var item = op_code.items.fisrt();
			if (rtl::isString(item)) return classof WidgetText;
			if (VirtualDom::isComponent(op_code.name)) return classof WidgetComponent;
			return classof WidgetTag;
		}
		return "";
	}
	
	
	/**
	 * Create strategy
	 */
	static CodeStrategyInterface createStrategy(var op_code)
	{
		if (op_code instanceof BaseOpCode) return new OpCodeStrategy(op_code);
		if (op_code instanceof VirtualDom) return new VirtualDomStrategy(op_code);
		return null;
	}
	
	
	/**
	 * Create widget
	 */
	static Widget createWidget(var op_code)
	{
		Widget widget = null;
		string widget_name = static::getWidgetName(op_code);
		if (widget_name)
		{
			widget = rtl::newInstance(widget_name, [this]);
			widget.setCode(op_code);
		}
		return widget;
	}
	
	
	/**
	 * Returns widget by path
	 */
	Widget get(Vector path)
	{
		if (not path) return null;
		if (path.count() == 0) return this;
		
		int pos = path.first();
		Widget item = this.items.get(pos);
		if (item) return item.get(path.slice(1));
		return null;
	}
	
	
	/**
	 * Returns current path
	 */
	Vector getCurrentPath()
	{
		Vector path = [];
		Widget widget = this;
		while (widget != null)
		{
			if (widget.parent_widget)
			{
				int index = widget.parent_widget.items.findIndex(bool (Widget item) => item == widget);
				path.push(index);
			}
			widget = widget.parent_widget;
		}
		return path.reverse();
	}
	
	
	/**
	 * Set parent widget
	 */
	void setParentWidget(Widget widget)
	{
		this.parent_widget = widget;
		this.page_model = widget.page_model;
	}
	
	
	/**
	 * Set page model
	 */
	void setPageModel(EditorPageModel page_model)
	{
		this.page_model = page_model;
	}
	
	
	/**
	 * Create widget
	 */
	void constructor()
	{
		parent();
		
		/* Create tree item */
		this.tree_item = new TreeItem();
		this.tree_item.widget = this;
	}
	
	
	/**
	 * Set op code
	 */
	void setCode(var op_code)
	{
		this.op_code = op_code;
		this.code_strategy = static::createStrategy(op_code);
	}
	
	
	/**
	 * Init widget
	 */
	void init()
	{
		this.page_model.widget_id = this.page_model.widget_id + 1;
		
		/* Setup id */
		this.id = this.page_model.widget_id;
		this.tree_item.key = this.id;
	}
	
	
	/**
	 * On select widget
	 */
	void onSelect()
	{
	}
	
	
	/**
	 * Insert widget
	 */
	void insert(string index, Widget widget)
	{
		/* Insert code */
		this.code_strategy.insert(index, widget.op_code);
		
		/* Insert item */
		this.items.insert(index, widget);
		this.tree_item.items.insert(index, widget.tree_item);
		widget.setParentWidget(this);
	}
	
	
	/**
	 * Remove widget
	 */
	void remove()
	{
		int index = this.parent_widget.items.findIndex(bool (Widget item) => item == this);
		if (index >= 0) this.parent_widget.removeItem(index);
	}
	
	
	/**
	 * Remove item
	 */
	void removeItem(int index)
	{
		this.code_strategy.remove(index);
		this.items.remove(index);
		this.tree_item.items.remove(index);
		if (this.page_model.selected_widget == this)
		{
			this.page_model.selectItem(null);
		}
		this.page_model.updateSelectedBox();
	}
}