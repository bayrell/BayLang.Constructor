/*!
 *  BayLang Constructor
 */

namespace BayLang.Constructor.Frontend.Editor.Widget;

use Runtime.BaseObject;
use BayLang.Constructor.Frontend.Editor.Parameters.Parameter;
use BayLang.Constructor.Frontend.Editor.Processor.CodeProcessor;
use BayLang.Constructor.Frontend.Editor.Widget.WidgetComponent;
use BayLang.Constructor.Frontend.Editor.Widget.WidgetTag;
use BayLang.Constructor.Frontend.Editor.WidgetEditPageModel;
use BayLang.Constructor.Frontend.Editor.WidgetTreeItem;
use BayLang.OpCodes.BaseOpCode;
use BayLang.OpCodes.OpHtmlItems;
use BayLang.OpCodes.OpHtmlTag;


class Widget extends BaseObject
{
	BaseOpCode code = null;
	BaseOpCode parent_code = null;
	WidgetEditPageModel page_model = null;
	WidgetTreeItem tree_item = null;
	Collection<Parameter> params = [];
	
	
	/**
	 * Constructor
	 */
	public void constructor(WidgetEditPageModel page_model, BaseOpCode code)
	{
		parent();
		this.code = code;
		this.page_model = page_model;
	}
	
	
	/**
	 * Can insert widget
	 */
	bool canInsert(Widget widget)
	{
		return false;
	}
	
	
	/**
	 * Update tree item
	 */
	void updateTreeItem()
	{
		if (not this.tree_item)
		{
			this.tree_item = new WidgetTreeItem(this.code);
		}
		
		/* Update label */
		this.tree_item.updateLabel();
	}
	
	
	/**
	 * Update tree items
	 */
	void updateTreeItems()
	{
		/* Create tree item if does not exists */
		if (not this.tree_item)
		{
			this.updateTreeItem();
		}
		
		/* Update items */
		this.tree_item.items = [];
		if (this.code.items instanceof OpHtmlItems)
		{
			for (int i=0; i<this.code.items.count(); i++)
			{
				BaseOpCode op_code_item = this.code.items.get(i);
				Widget widget_item = this.page_model.widget_manager.get(op_code_item);
				this.tree_item.items.push(widget_item.tree_item);
			}
		}
	}
	
	
	/**
	 * Setup
	 */
	void setup()
	{
		if (this.is_initialized) return;
		
		/* Setup settings */
		this.setupSettings();
		
		/* Setup params */
		this.setupParams();
		
		/* Init params */
		this.initParams();
		
		/* Set flag initialized */
		this.is_initialized = true;
	}
	
	
	/**
	 * Setup settings
	 */
	void setupSettings()
	{
	}
	
	
	/**
	 * Setup params
	 */
	void setupParams()
	{
	}
	
	
	/**
	 * Init params
	 */
	void initParams()
	{
		this.params.each(
			void (Parameter param)
			{
				param.init();
			}
		);
	}
	
	
	/**
	 * Create new instance
	 */
	public static Widget newInstance(WidgetEditPageModel page_model, BaseOpCode op_code)
	{
		if (not (op_code instanceof OpHtmlTag)) return null;
		
		string selected_tag_name = op_code.tag_name;
		if (CodeProcessor::isComponent(selected_tag_name))
		{
			return new WidgetComponent(page_model, op_code);
		}
		
		return new WidgetTag(page_model, op_code);
	}
}