/*!
 *  BayLang Technology
 *
 *  (c) Copyright 2016-2024 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace BayLang.Constructor.Frontend.Editor.Widget;

use Runtime.BaseObject;
use BayLang.Constructor.Frontend.Editor.Styles.Style;
use BayLang.Constructor.Frontend.Editor.Widget.Widget;
use BayLang.OpCodes.BaseOpCode;
use BayLang.OpCodes.OpHtmlAttribute;
use BayLang.OpCodes.OpHtmlTag;
use BayLang.OpCodes.OpString;


class WidgetStyle extends BaseObject
{
	Widget widget = null;
	Vector items = [];
	OpString class_expression = null;
	string current_style = "";
	
	
	/**
	 * Constructor
	 */
	void constructor(Widget widget)
	{
		parent();
		this.widget = widget;
	}
	
	
	/**
	 * Init class name
	 */
	void init()
	{
		if (not (this.widget.op_code instanceof OpHtmlTag)) return;
		
		OpHtmlAttribute class_name = this.widget.op_code.attrs.find(
			bool (OpHtmlAttribute item) => item.key == "class"
		);
		if (not class_name) return;
		if (not (class_name.expression instanceof OpString)) return;
		
		this.class_expression = class_name.expression;
		this.items = rs::split(" ", class_name.expression.value);
		this.current_style = this.items.first("");
	}
	
	
	/**
	 * Update class name
	 */
	void update()
	{
		if (not this.class_expression) return;
		this.class_expression.value = rs::join(" ", this.items);
	}
	
	
	/**
	 * Update current style
	 */
	void updateCurrentStyle(string content)
	{
		Style item = this.widget.page_model.styles.findStyle(this.current_style);
		if (item)
		{
			item.setContent(content);
		}
		else if (this.current_style != "")
		{
			Style item = new Style();
			item.setClassName(this.current_style);
			item.setContent(content);
			this.widget.page_model.styles.addItem(item);
		}
		window.setTimeout(void(){ this.widget.page_model.page_widget.updateSelectedBox(); }, 1);
	}
	
	
	/**
	 * Add class
	 */
	void add(string class_name)
	{
		int index = this.items.indexOf(class_name);
		if (index != -1) return;
		
		Style item = new Style();
		item.setClassName(class_name);
		this.widget.page_model.styles.addItem(item);
		this.items.push(class_name);
		this.current_style = class_name;
		
		/* Update class names */
		this.update();
	}
	
	
	/**
	 * Rename class
	 */
	void rename(int index, string new_name)
	{
		/* Find style */
		string old_name = this.items.get(index);
		if (old_name == new_name) return;
		
		/* Check new name */
		int new_index = this.items.indexOf(new_name);
		if (new_index != -1) return;
		
		/* Copy style */
		if (not this.widget.page_model.styles.exists(new_name))
		{
			this.widget.page_model.styles.copy(old_name, new_name);
		}
		
		/* Rename */
		this.items.set(index, new_name);
		if (this.current_style == old_name) this.current_style = new_name;
		
		/* Update class names */
		this.update();
	}
	
	
	/**
	 * Remove class
	 */
	void remove(int index)
	{
		string old_name = this.items.get(index);
		
		/* Remove class */
		if (this.current_style == old_name) this.current_style = "";
		this.items.remove(index);
		
		/* Update class name */
		this.update();
	}
}