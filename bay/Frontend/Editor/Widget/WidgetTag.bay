/*!
 *  BayLang Constructor
 */

namespace BayLang.Constructor.Frontend.Editor.Widget;

use Runtime.lib;
use BayLang.Constructor.Frontend.Editor.Parameters.Parameter;
use BayLang.Constructor.Frontend.Editor.Parameters.ParameterClassName;
use BayLang.Constructor.Frontend.Editor.Parameters.ParameterComponent;
use BayLang.Constructor.Frontend.Editor.Parameters.ParameterWidgetName;
use BayLang.Constructor.Frontend.Editor.Styles.Selector;
use BayLang.Constructor.Frontend.Editor.Widget.Widget;
use BayLang.Constructor.Frontend.Editor.WidgetEditPageModel;
use BayLang.OpCodes.BaseOpCode;
use BayLang.OpCodes.OpHtmlAttribute;
use BayLang.OpCodes.OpHtmlContent;
use BayLang.OpCodes.OpHtmlTag;
use BayLang.OpCodes.OpHtmlValue;
use BayLang.OpCodes.OpString;


class WidgetTag extends Widget
{
	/* Params */
	string tag_name = "";
	Parameter param_class_name = null;
	Parameter param_widget_name = null;
	
	/* Widget content */
	bool is_raw = false;
	string html_content = "";
	
	
	/**
	 * Returns true if component
	 */
	bool isComponent() => false;
	
	
	/**
	 * Returns true if tag has model
	 */
	bool isModel() => false;
	
	
	/**
	 * Returns widget name
	 */
	string getName() => this.param_widget_name.value;
	
	
	/**
	 * Returns CSS Content
	 */
	string getSelectorName() => "." ~ this.param_widget_name.value;
	
	
	/**
	 * Returns selector
	 */
	Selector getSelector()
	{
		string selector_name = this.getSelectorName();
		return this.page_model.styles.getSelector(selector_name);
	}
	
	
	/**
	 * Can insert widget
	 */
	bool canInsert(Widget widget)
	{
		if (not this.settings) return false;
		return this.settings.canInsert(widget);
	}
	
	
	/**
	 * Constructor
	 */
	public void constructor(WidgetEditPageModel page_model, BaseOpCode code)
	{
		parent(page_model, code);
		
		/* Tag name */
		this.tag_name = code.tag_name;
		
		/* Create params */
		this.param_class_name = new ParameterClassName
		{
			"widget": this,
		};
		this.param_widget_name = new ParameterWidgetName
		{
			"widget": this,
		};
		this.param_class_name.param_widget_name = this.param_widget_name;
		this.param_widget_name.param_class_name = this.param_class_name;
		this.param_class_name.op_attr = null;
		this.param_class_name.value = [];
		this.param_widget_name.op_attr = null;
		this.param_widget_name.value = "";
	}
	
	
	/**
	 * Setup settings
	 */
	void setupSettings()
	{
		if (this.settings != null) return;
		BaseOpCode op_code = this.code;
		
		/* Find component settings */
		this.settings = this.page_model.getFrameEditor()
			.getWidgetSettings(op_code.tag_name);
	}
	
	
	/**
	 * Setup params
	 */
	void setupParams()
	{
		/* Clear params */
		this.params = [];
		this.params.add(this.param_widget_name);
		this.params.add(this.param_class_name);
		
		/* if settings is exists */
		this.setupParamsFromSettings();
		
		/* Setup content */
		this.setupContent();
	}
	
	
	/**
	 * Setup HTML Content
	 */
	void setupContent()
	{
		if (not this.code.items) return;
		
		/* Get code */
		BaseOpCode text_item = this.code.items.items.get(0);
		
		/* Setup text */
		this.html_content = "";
		if (text_item instanceof OpHtmlContent)
		{
			this.html_content = text_item.value;
		}
		else if (text_item instanceof OpHtmlValue)
		{
			this.is_raw = text_item.kind == "raw";
			if (text_item.value instanceof OpString)
			{
				this.html_content = text_item.value.value;
			}
		}
	}
	
	
	/**
	 * Change HTML Raw Flag
	 */
	void changeHtmlRaw(string is_raw)
	{
		/* Set value */
		this.is_raw = is_raw == "1";
		
		/* Update key debug attr */
		OpHtmlAttribute attr_item = this.code.attrs.findItem(
			lib::equalAttr("key", "@key_debug")
		);
		if (attr_item)
		{
			if (this.is_raw)
			{
				if (rs::indexOf(attr_item.value.value, "_raw") == -1)
				{
					attr_item.value.value ~= "_raw";
				}
			}
			else
			{
				if (rs::indexOf(attr_item.value.value, "_raw") != -1)
				{
					attr_item.value.value = rs::replace("_raw", "", attr_item.value.value);
				}
			}
		}
		
		/* Set content */
		BaseOpCode code = null;
		if (this.is_raw)
		{
			code = new OpHtmlValue
			{
				"kind": "raw"
				"value": new OpString
				{
					"value": this.html_content,
				},
			};
		}
		else
		{
			code = new OpHtmlContent
			{
				"value": this.html_content,
			};
		}
		
		/* Add content */
		this.code.items.items = [];
		this.code.items.items.push(code);
	}
	
	
	/**
	 * Change HTML content
	 */
	void changeHtmlContent(string content)
	{
		/* Get code */
		BaseOpCode text_item = this.code.items.items.get(0);
		
		/* Set value */
		this.html_content = content;
		
		/* Set content */
		if (text_item instanceof OpHtmlContent)
		{
			text_item.value = this.html_content;
		}
		else if (text_item instanceof OpHtmlValue)
		{
			if (text_item.value instanceof OpString)
			{
				text_item.value.value = this.html_content;
			}
		}
	}
	
	
	/**
	 * Setup attrs
	 */
	void setupAttrs()
	{
		Collection<OpHtmlAttribute> attrs = this.code.attrs;
		if (not attrs) return;
		
		/* Setup paremeters values */
		for (int i=0; i<attrs.count(); i++)
		{
			OpHtmlAttribute op_attr = attrs.get(i);
			for (int j=0; j<this.params.count(); j++)
			{
				Parameter param = this.params.get(j);
				if (
					param instanceof ParameterComponent and
					param.isOpCode(op_attr)
				)
				{
					param.setOpCode(op_attr);
				}
			}
		}
		
		/* Create class attribute */
		if (this.param_class_name.op_attr == null)
		{
			OpHtmlAttribute op_attr = new OpHtmlAttribute
			{
				"key": "class",
			};
			this.param_class_name.op_attr = op_attr;
			this.param_widget_name.op_attr = op_attr;
		}
	}
}