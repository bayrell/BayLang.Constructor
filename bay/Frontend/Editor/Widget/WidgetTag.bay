/*!
 *  BayLang Technology
 *
 *  (c) Copyright 2016-2024 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace BayLang.Constructor.Frontend.Editor.Widget;

use Runtime.VirtualDom;
use BayLang.Constructor.Frontend.Editor.Widget.Widget;
use BayLang.Constructor.Frontend.Editor.Widget.WidgetStyle;
use BayLang.OpCodes.BaseOpCode;
use BayLang.OpCodes.OpHtmlAttribute;
use BayLang.OpCodes.OpHtmlItems;
use BayLang.OpCodes.OpHtmlTag;
use BayLang.OpCodes.OpString;


class WidgetTag extends Widget
{
	Vector<Widget> items = [];
	WidgetStyle styles = new WidgetStyle(this);
	
	
	/**
	 * Returns item
	 */
	Widget get(Vector path)
	{
		if (path.count() == 0) return this;
		
		int pos = path.first();
		Widget item = this.items.get(pos);
		if (item) return item.get(path.slice(1));
		return null;
	}
	
	
	/**
	 * Init items
	 */
	void initItems()
	{
		this.items = [];
		if (this.op_code and this.op_code.content)
		{
			for (int i=0; i<this.op_code.content.count(); i++)
			{
				BaseOpCode item = this.op_code.content.get(i);
				Widget widget = this.createWidget(item);
				if (widget) this.items.push(widget);
			}
		}
		
		/* Init tree items */
		this.tree_item.items = [];
		for (int i=0; i<this.items.count(); i++)
		{
			Widget widget = this.items.get(i);
			this.tree_item.items.push(widget.tree_item);
		}
	}
	
	
	/**
	 * Update tree label
	 */
	void updateTreeLabel()
	{
		this.tree_item.label = this.code_strategy.getTagName();
		
		/* Add class name */
		string class_name = rs::join(".", this.styles.items);
		if (class_name)
		{
			this.tree_item.label ~= "." ~ class_name;
		}
	}
	
	
	/**
	 * Init widget
	 */
	void init()
	{
		this.initItems();
		this.styles.init();
		
		/* Init label */
		this.updateTreeLabel();
	}
	
	
	/**
	 * Set tag name
	 */
	void setTagName(string value)
	{
		this.code_strategy.setTagName(value);
	}
	
	
	/**
	 * Insert widget
	 */
	void insert(string index, Widget widget)
	{
		/* Insert code */
		this.code_strategy.insert(index, widget.op_code);
		
		/* Insert item */
		this.items.insert(index, widget);
		this.tree_item.items.insert(index, widget.tree_item);
	}
	
	
	/**
	 * Remove item
	 */
	void removeItem(int index)
	{
		this.code_strategy.remove(index);
		this.items.remove(index);
		this.tree_item.items.remove(index);
		if (this.page_model.selected_widget == this)
		{
			this.page_model.selectItem(null);
		}
		this.page_model.updateSelectedBox();
	}
}