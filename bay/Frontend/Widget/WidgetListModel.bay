/*!
 *  BayLang Constructor
 */

namespace BayLang.Constructor.Frontend.Widget;

use Runtime.Callback;
use Runtime.Entity.Factory;
use Runtime.Web.BasePageModel;
use Runtime.Web.ModelFactory;
use Runtime.Web.RenderContainer;
use Runtime.Web.Messages.ClickMessage;
use Runtime.Widget.Form.FormDeleteStorage;
use Runtime.Widget.Form.FormModel;
use Runtime.Widget.Form.FormSubmitStorage;
use Runtime.Widget.Table.TableDialogModel;
use Runtime.Widget.Label;
use Runtime.Widget.RowButtonsModel;
use BayLang.Constructor.Frontend.Widget.WidgetList;


class WidgetListModel extends BasePageModel
{
	string component = classof WidgetList;
	string project_id = "";
	string module_id = "";
	
	
	/**
	 * Init widget settings
	 */
	void initWidget(Dict params)
	{
		parent(params);
		
		/* Set route matches */
		this.project_id = this.layout.route.matches.get("project_id");
		this.module_id = this.layout.route.matches.get("module_id");
		
		/* Add top buttons */
		this.top_buttons = this.addWidget(
			classof RowButtonsModel,
			{
				"widget_name": "top_buttons",
				"buttons":
				[
					{
						"content": "Add",
						"widget_name": "add_button",
						"styles": [
							"success",
						]
					},
				],
				"events":
				{
					"click": new Callback(this, "onTopButtonsClick"),
				},
			}
		);
		
		/* Add table */
		this.table = this.addWidget(
			classof TableDialogModel,
			{
				"widget_name": "table",
				"get_title": void (Dict params)
				{
					string action = params.get("action");
					Dict item = params.get("item");
					if (action == "add")
					{
						return "Add widget";
					}
					if (action == "edit")
					{
						return "Edit widget " ~ item.get("id");
					}
					if (action == "delete")
					{
						return "Delete widget " ~ item.get("id");
					}
					return "";
				},
				"styles": ["border"],
				
				/* Table storage */
				"storage": new Factory
				(
					"Runtime.Widget.Table.TableStorage",
					{
						"api_name": "baylang.constructor.widget::search",
					}
				),
				
				/* Add form */
				"add_form": new ModelFactory(
					classof FormModel,
					{
						"widget_name": "add_form",
						"primary_key": ["id"],
						"foreign_key":
						{
							"project_id": this.project_id,
							"module_id": this.module_id,
						},
						"storage": new Factory(
							classof FormSubmitStorage,
							{
								"api_name": "baylang.constructor.widget::save"
							}
						),
						"fields":
						[
							{
								"name": "id",
								"label": "Name",
								"component": "Runtime.Widget.Input",
								"default": this.module_id ~ ".WidgetModel",
							}
						],
					}
				),
				
				/* Delete form */
				"delete_form": new ModelFactory(
					classof FormModel,
					{
						"widget_name": "delete_form",
						"primary_key": ["id"],
						"foreign_key":
						{
							"project_id": this.project_id,
							"module_id": this.module_id,
						},
						"storage": new Factory(
							classof FormDeleteStorage,
							{
								"api_name": "baylang.constructor.widget::save"
							}
						),
					}
				),
				
				/* Foreign key */
				"foreign_key": {
					"project_id": this.project_id,
					"module_id": this.module_id,
				},
				
				/* Table fields */
				"fields":
				[
					{
						"name": "row_number",
					},
					{
						"name": "id",
						"label": "Widget name",
						"component": "Runtime.Widget.Label",
					},
					{
						"name": "row_buttons",
						"model": new ModelFactory(
							"Runtime.Widget.RowButtonsModel",
							{
								"buttons":
								[
									{
										"widget_name": "edit",
										"content": "Edit",
										"href": void (Dict data)
										{
											Dict item = data.get("item");
											return this.layout.url(
												"baylang:project:widget:edit",
												{
													"project_id": this.project_id,
													"module_id": this.module_id,
													"widget_name": item.get("id"),
												}
											);
										},
										"styles":
										[
											"default",
											"small"
										],
									},
									{
										"widget_name": "delete",
										"content": "Delete",
										"events":
										{
											"click": new Callback(this, "onDeleteClick"),
										},
										"styles":
										[
											"danger",
											"small"
										],
									}
								],
							}
						),
					},
				],
			}
		);
	}
	
	
	/**
	 * Build title
	 */
	void buildTitle(RenderContainer container)
	{
		this.layout.setPageTitle("Widgets");
	}
	
	
	/**
	 * Delete click
	 */
	void onDeleteClick(ClickMessage message)
	{
		this.table.delete_dialog.showDelete(message.data.get("item"));
	}
	
	
	/**
	 * Top buttons click
	 */
	void onTopButtonsClick(ClickMessage message)
	{
		if (message.widget.widget_name == "add_button")
		{
			this.table.save_dialog.showAdd();
		}
	}
}