/*!
 *  BayLang Constructor
 */

namespace BayLang.Constructor.Backend.Lib;

use Runtime.fs;
use Runtime.BaseObject;
use BayLang.Constructor.Backend.Lib.Module;


class Project extends BaseObject
{
	string project_path = "";
	Dict project_info = null;
	Dict<Module> modules = null;
	
	
	/**
	 * Read projects
	 */
	async static Collection<Project> readProjects(string projects_path)
	{
		if (not(await fs::isFolder(projects_path))) return [];
		
		Vector<string> result = [];
		Collection<string> items = await fs::listDir(projects_path);
		for (int i=0; i<items.count(); i++)
		{
			string file_name = items.get(i);
			Project project = await static::readProject(fs::join([projects_path, file_name]));
			if (project) result.push(project);
		}
		return result;
	}
	
	
	/**
	 * Read project from folder
	 */
	async static Project readProject(string project_path)
	{
		Project project = new Project();
		await project.initFromFile(project_path);
		if (not project.exists()) return null;
		return project;
	}
	
	
	/**
	 * Init from file
	 */
	async void initFromFile(string project_path)
	{
		this.module_info = null;
		string file_name = rs::basename(project_path);
		
		if (file_name == "") return;
		if (file_name[0] == ".") return;
		
		string project_json_path = fs::join([project_path, "project.json"]);
		if (not(await fs::isFolder(project_path))) return;
		if (not(await fs::isFile(project_json_path))) return;
		
		/* Read file */
		string content = await fs::readFile(project_json_path);
		Dict project_info = rtl::json_decode(content);
		
		/* Init project */
		this.init(project_path, project_info);
	}
	
	
	/**
	 * Init project
	 */
	void init(string project_path, Dict project_info)
	{
		this.project_info = null;
		
		if (not project_info) return;
		if (not project_info.has("name")) return;
		
		this.project_path = project_path;
		this.project_info = project_info;
	}
	
	
	/**
	 * Returns true if project is exists
	 */
	void exists() => this.project_info != null;
	
	
	/**
	 * Returns project path
	 */
	string getPath() => this.exists() ? this.project_path : "";
	
	
	/**
	 * Returns project file_name
	 */
	string getID() => this.exists() ? rs::basename(this.project_path) : "";
	
	
	/**
	 * Returns project name
	 */
	string getName() => this.exists() ? this.project_info.get("name") : "";
	
	
	/**
	 * Set project name
	 */
	void setName(string name)
	{
		if (not this.exists()) return;
		this.project_info.set("name", name);
	}
	
	
	/**
	 * Returns project description
	 */
	string getDescription() => this.exists() ? this.project_info.get("description") : "";
	
	
	/**
	 * Set project description
	 */
	void setDescription(string description)
	{
		if (not this.exists()) return;
		this.project_info.set("description", description);
	}
	
	
	/**
	 * Returns project type
	 */
	string getType() => this.exists() ? this.project_info.get("type") : "";
	
	
	/**
	 * Set project type
	 */
	void setType(string project_type)
	{
		if (not this.exists()) return;
		this.project_info.set("type", project_type);
	}
	
	
	/**
	 * Save project
	 */
	async void saveInfo()
	{
		string project_json_path = fs::join([this.project_path, "project.json"]);
		string content = rtl::json_encode(this.project_info, rtl::JSON_PRETTY);
		await fs::saveFile(project_json_path, content);
	}
	
	
	/**
	 * Read modules
	 */
	void readModules()
	{
		if (not this.exists()) return;
		
		this.modules = {};
		
		/* Read sub modules */
		this.readSubModules(this.project_path, this.project_info.get("modules"));
	}
	
	
	/**
	 * Read sub modules
	 */
	void readSubModules(string path, Collection<Dict> items)
	{
		if (not items) return;
		for (int i=0; i<items.count(); i++)
		{
			Dict item = items.get(i);
			string module_src = item.get("src");
			string module_type = item.get("type");
			string folder_path = fs::join([ path, module_src ]);
			
			/* Read from folder */
			if (module_type == "folder")
			{
				this.readModuleFromFolder(folder_path);
			}
			
			/* Read module */
			else if (module_type == "folder")
			{
				Module module = Module::readModule(this, folder_path);
				if (module)
				{
					/* Set module */
					this.modules.set(module.getName(), module);
					
					/* Read sub modules */
					this.readSubModules(module.getPath(), module.module_info.get("modules"));
				}
			}
		}
	}
	
	
	/**
	 * Read sub modules
	 */
	void readModuleFromFolder(string folder_path)
	{
		Collection<string> items = await fs::listDir(folder_path);
		for (int i=0; i<items.count(); i++)
		{
			string file_name = items.get(i);
			
			/* Read module */
			Module module = Module::readModule(this, fs::join([folder_path, file_name]));
			if (module)
			{
				/* Set module */
				this.modules.set(module.getName(), module);
				
				/* Read sub modules */
				this.readSubModules(module.getPath(), module.module_info.get("modules"));
			}
		}
	}
}