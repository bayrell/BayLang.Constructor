/*!
 *  BayLang Technology
 *
 *  (c) Copyright 2016-2024 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace BayLang.Constructor.Backend.Service;

use Runtime.fs;
use Runtime.File;
use Runtime.Exceptions.ApiError;
use Runtime.Exceptions.ItemNotFound;
use Runtime.Exceptions.RuntimeException;
use Runtime.Widget.Crud.CrudService;
use Runtime.Widget.Crud.Rules.MatchRule;
use Runtime.Widget.Crud.Rules.Required;
use BayLang.Exceptions.ParserUnknownError;
use BayLang.Constructor.Backend.ApiHook;
use BayLang.Helper.Module;
use BayLang.Helper.Project;


class FontService extends CrudService
{
	Project project;
	string assets_path = "";
	string fonts_path = "";
	
	
	/**
	 * Init rules
	 */
	void initRules()
	{
		this.rules.addRules([
			new MatchRule{"name": "api_name", "regular": MatchRule::ALPHA_NUMERIC_DASH},
			new Required{"name": "name"},
		]);
	}
	
	
	/**
	 * Returns save fields
	 */
	Collection getSaveFields() =>
	[
		"name",
		"css",
	];
	
	
	/**
	 * Returns primary key
	 */
	Dict getPrimaryKey(Dict item) =>
	{
		"name": item.get("name"),
	};
	
	
	/**
	 * Load project
	 */
	async void loadProject(string project_id)
	{
		this.project = await ApiHook::getProject(project_id);
		if (not this.project)
		{
			throw new ApiError(new ItemNotFound(project_id, "Project"));
		}
		
		/* Load project */
		await this.project.load();
		
		/* Get fonts path */
		this.assets_path = ApiHook::getAssetsPath(this.project);
		this.fonts_path = fs::join([this.assets_path, "fonts"]);
	}
	
	
	/**
	 * New item
	 */
	async void newItem() =>
	{
		"exists": false,
		"files": [],
	};
	
	
	/**
	 * Find item
	 */
	async void findItem(Dict pk)
	{
		if (pk == null) return null;
		
		string font_name = pk.get("name")
		string font_path = fs::join([this.fonts_path, font_name]);
		if (not await fs::isDir(font_path))
		{
			return null;
		}
		
		return {
			"exists": true,
			"name": font_name,
			"path": font_path,
			"files": [],
		};
	}
	
	
	/**
	 * Load item
	 */
	async void loadItem(Dict pk, bool create_instance = true)
	{
		await parent(pk, create_instance);
		
		/* Load css */
		string folder_path = this.item.get("path");
		string file_css_path = fs::join([folder_path, "style.scss"]);
		if (await fs::exists(file_css_path))
		{
			string css_content = await fs::readFile(file_css_path);
			this.item.set("css", css_content);
		}
		
		/* Load files */
		if (await fs::exists(folder_path))
		{
			Collection files = await fs::listDir(folder_path);
			files = files.map(Dict (string name) => {"name": name});
			this.item.set("files", files);
		}
	}
	
	
	/**
	 * Load items
	 */
	async void loadItems()
	{
		/* Get items */
		this.items = [];
		this.page = 0;
		this.pages = 1;
		if (await fs::isDir(this.fonts_path))
		{
			this.items = await fs::listDir(this.fonts_path);
			this.items = this.items.map(void (string name) => {
				"name": name,
			});
		}
	}
	
	
	/**
	 * Rebuild assets
	 */
	async void rebuild()
	{
		if (not await fs::isDir(this.fonts_path)) return;
		
		/* Get items */
		Collection items = [];
		items = await fs::listDir(this.fonts_path);
		
		/* Read styles */
		Collection<string> content = [
			"<!--",
			" *  Fonts",
			"-->",
			"",
			"<class name=\"App.Components.Blocks.Fonts\">",
			"<style>",
		];
		for (int i=0; i<items.count(); i++)
		{
			string font_name = items.get(i);
			string file_css_path = fs::join([this.fonts_path, font_name, "style.scss"]);
			if (await fs::exists(file_css_path))
			{
				string css_content = await fs::readFile(file_css_path);
				content.push(css_content);
			}
		}
		content.push("</style>");
		content.push("</class>");
		
		/* Find module */
		string file_path = fs::join([
			this.project.path,
			"src", "App", "bay", "Components", "Blocks", "Fonts.bay"
		]);
		Module module = this.project.findModuleByFileName(file_path);
		if (not module) return;
		
		/* Save styles */
		await fs::saveFile(file_path, rs::join("\n", content));
		
		/* Compile file */
		try
		{
			string relative_file_path = module.getRelativeSourcePath(file_path);
			await module.compile(relative_file_path);
		}
		catch (ParserUnknownError e)
		{
			string error_str = e.getErrorMessage();
			string line = e.getErrorLine();
			string pos = e.getErrorPos();
			if (line != -1)
			{
				error_str ~= " at Ln:" ~ line ~ ( (pos != "") ? ", Pos:" ~ pos : "" );
			}
			throw new ApiError(new RuntimeException(error_str));
		}
		
		/* Update assets */
		await module.updateAssets();
	}
	
	
	/**
	 * Save
	 */
	async void saveItem()
	{
		string font_key = this.pk ? this.pk.get("name") : "";
		string font_name = this.item.get("name");
		string font_path = fs::join([this.fonts_path, font_name]);
		
		/* Check folder is exists */
		bool exists = this.item.get("exists");
		if (exists)
		{
			/* Rename folder */
			if (font_key != font_name)
			{
				string font_path_old = fs::join([this.fonts_path, font_key]);
				await fs::rename(font_path_old, font_path);
			}
		}
		else
		{
			/* Create folder */
			await fs::mkDir(font_path);
		}
		
		/* Save css */
		string css_content = this.item.get("css");
		if (css_content == "")
		{
			css_content = rs::join("\n", [
				"@font-face {",
				"  font-family: '" ~ font_name ~ "';",
				"  font-weight: normal;",
				"  font-style: normal;",
				"  src: url(${ Component::assets('" ~ font_name ~ "/" ~ font_name ~ ".ttf') });",
				"}"
			]);
		}
		string file_css_path = fs::join([font_path, "style.scss"]);
		await fs::saveFile(file_css_path, css_content);
		
		/* Rebuild assets */
		await this.rebuild();
	}
	
	
	/**
	 * Delete item
	 */
	async void deleteItem()
	{
		string font_name = this.item.get("name");
		string font_path = fs::join([this.fonts_path, font_name]);
		
		#ifcode PHP then
		shell_exec("rm -rf $font_path");
		#endif
	}
	
	
	/**
	 * Upload file
	 */
	async void uploadFile(File file)
	{
		/* Create file */
		string folder_path = this.item.get("path");
		string file_upload_path = fs::join([folder_path, file.getName()]);
		string content = await fs::readFile(file.getPath());
		await fs::saveFile(file_upload_path, content);
	}
	
	
	/**
	 * Delete file
	 */
	async void deleteFile(string file_name)
	{
		/* Upload file */
		string folder_path = this.item.get("path");
		string file_path = fs::join([folder_path, file_name]);
		
		/* Remove file */
		if (await fs::exists(file_path))
		{
			await fs::unlink(file_path);
		}
	}
}