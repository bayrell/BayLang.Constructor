/*!
 *  BayLang Technology
 *
 *  (c) Copyright 2016-2024 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace BayLang.Constructor.Backend.Api;

use Runtime.fs;
use Runtime.Exceptions.ApiError;
use Runtime.Exceptions.ItemNotFound;
use Runtime.Exceptions.RuntimeException;
use Runtime.Serializer.JsonType;
use Runtime.Serializer.MapType;
use Runtime.Serializer.StringType;
use Runtime.Web.BaseApi;
use Runtime.Web.Annotations.ApiMethod;
use BayLang.Constructor.Backend.ApiHook;
use BayLang.Constructor.Backend.Api.Widget;
use BayLang.Constructor.Backend.Service.WidgetService;
use BayLang.OpCodes.BaseOpCode;
use BayLang.OpCodes.OpCodeType;
use BayLang.Helper.Module;
use BayLang.Helper.Project;


class WidgetApi extends BaseApi
{
	/**
	 * Returns api name
	 */
	static string getApiName() => "baylang.constructor.widget";
	
	
	/**
	 * Returns data rules
	 */
	void getDataRules(MapType rules)
	{
		rules.addType("widget_name", new StringType());
		rules.addType("code_model", new JsonType(new OpCodeType()));
		rules.addType("code_component", new JsonType(new OpCodeType()));
	}
	
	
	/**
	 * Load widget
	 */
	@ApiMethod{ "name": "load" }
	async void getOpCode()
	{
		/* Filter data */
		this.filterData();
		
		/* Get widget name */
		string widget_name = this.data.get("widget_name");
		
		/* Get project */
		Project project = ApiHook::getProject();
		if (not project)
		{
			throw new ApiError(new ItemNotFound("project"))
		}
		
		/* Find widget */
		Widget widget = await Widget::findWidget(project, widget_name);
		if (widget == null)
		{
			throw new ApiError(new ItemNotFound(widget_name, "widget"));
		}
		
		/* Load opcode */
		await widget.loadModel();
		await widget.loadComponent();
		
		/* Success */
		this.success({
			"data": {
				"widget_name": widget_name,
				"model_file_name": widget.model_file_name,
				"model_name": widget.model_name,
				"model": widget.model,
				"component_file_name": widget.component_file_name,
				"component_name": widget.component_name,
				"component": widget.component,
			},
		});
	}
	
	
	/**
	 * Save widget
	 */
	@ApiMethod{ "name": "save" }
	async void save()
	{
		/* Filter data */
		this.filterData();
		
		/* Get code */
		BaseOpCode code_model = this.data.get("code_model");
		BaseOpCode code_component = this.data.get("code_component");
		if (not code_component)
		{
			throw new ApiError(new ItemNotFound("code_component"));
		}
		
		/* Get project */
		Project project = ApiHook::getProject();
		if (not project)
		{
			throw new ApiError(new ItemNotFound("project"))
		}
		
		/* Find widget */
		string widget_name = this.data.get("widget_name");
		Widget widget = await Widget::findWidget(project, widget_name);
		if (widget == null)
		{
			throw new ApiError(new ItemNotFound(widget_name, "widget"));
		}
		
		/* Load opcode */
		await widget.loadModel();
		await widget.loadComponent();
		
		/* Save component */
		await widget.saveComponent(code_component);
		
		/* Success */
		this.success();
		
		return ;
		
		WidgetService service = new WidgetService();
		string project_id = this.post_data.get("project_id");
		string current_widget = this.post_data.get("current_widget");
		
		/* Load project */
		await service.loadProject(project_id);
		
		/* Load modules */
		await service.loadItem({ "id": current_widget });
		
		/* Load widget */
		await service.item.load();
		
		/* Get component path */
		string component_path = service.item.getComponentPath();
		if (not component_path)
		{
			throw new ApiError(
				new RuntimeException("Failed to load widget '" ~ current_widget ~ "'")
			);
		}
		
		/* Save file */
		await service.saveFile(service.item.getComponentPath(), this.post_data.get("component"));
		await service.saveFile(service.item.getModelPath(), this.post_data.get("model"));
		
		/* Update assets */
		await service.item.module.updateAssets();
		await ApiHook::updateAssets();
		
		/* Success */
		this.success();
	}
}