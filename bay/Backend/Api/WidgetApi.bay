/*!
 *  BayLang Constructor
 */

namespace BayLang.Constructor.Backend.Api;

use Runtime.fs;
use Runtime.Exceptions.ApiError;
use Runtime.Exceptions.ItemNotFound;
use Runtime.Exceptions.RuntimeException;
use Runtime.Web.BaseApi;
use Runtime.Web.Annotations.ApiMethod;
use BayLang.Helper.Module;
use BayLang.Helper.Project;
use BayLang.Helper.Widget;
use BayLang.OpCodes.BaseOpCode;


class WidgetApi extends BaseApi
{
	/**
	 * Returns api name
	 */
	pure string getApiName() => "admin.constructor.widget";
	
	
	/**
	 * Load widget
	 */
	@ApiMethod
	async void getOpCode()
	{
		string project_id = this.post_data.get("project_id");
		string current_widget = this.post_data.get("current_widget");
		
		/* Find project */
		Project project = await Project::readProject(
			fs::join([@.env("constructor_path"), "projects", project_id])
		);
		if (not project)
		{
			throw new ApiError( new ItemNotFound(project_id, "Project") );
			return;
		}
		
		/* Load project */
		await project.load();
		
		/* Find widget */
		Widget widget = await project.getWidget(current_widget);
		if (not widget)
		{
			throw new ApiError( new ItemNotFound(current_widget, "Widget") );
			return;
		}
		
		/* Load widget */
		await widget.load();
		
		/* Get component path */
		string component_path = widget.getComponetPath();
		if (not component_path)
		{
			throw new ApiError(
				new RuntimeException("Failed to load widget '" ~ current_widget ~ "'")
			);
			return;
		}
		
		/* Get widget op code */
		string model_content = await widget.getModelContent();
		string component_content = await widget.getComponentContent();
		BaseOpCode component_op_code = await widget.getComponentOpCode();
		
		/* Success */
		this.success({
			"data": {
				"model_content": model_content,
				"component_content": component_content,
				"component_op_code": component_op_code,
			},
		});
	}
	
	
	/**
	 * Save widget
	 */
	@ApiMethod
	async void save()
	{
		string project_id = this.post_data.get("project_id");
		string current_widget = this.post_data.get("current_widget");
		
		/* Find project */
		Project project = await Project::readProject(
			fs::join([@.env("constructor_path"), "projects", project_id])
		);
		if (not project)
		{
			throw new ApiError( new ItemNotFound(project_id, "Project") );
			return;
		}
		
		/* Load project */
		await project.load();
		
		/* Find widget */
		Widget widget = await project.getWidget(current_widget);
		if (not widget)
		{
			throw new ApiError( new ItemNotFound(current_widget, "Widget") );
			return;
		}
		
		/* Load widget */
		await widget.load();
		
		/* Get component path */
		string component_path = widget.getComponetPath();
		if (not component_path)
		{
			throw new ApiError(
				new RuntimeException("Failed to load widget '" ~ current_widget ~ "'")
			);
			return;
		}
		
		/* Save widget content */
		string file_path = widget.getComponetPath();
		string content = this.post_data.get("content");
		await fs::saveFile(file_path, content);
		
		/* Compile file */
		string relative_src_file_path = widget.module.getRelativeSourcePath(file_path);
		widget.module.compile(relative_src_file_path);
		
		/* Update assets */
		widget.module.updateAssets();
		
		/* Success */
		this.success();
	}
}