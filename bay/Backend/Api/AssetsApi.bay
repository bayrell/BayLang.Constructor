/*!
 *  BayLang Technology
 *
 *  (c) Copyright 2016-2024 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace BayLang.Constructor.Backend.Api;

use Runtime.fs;
use Runtime.File;
use Runtime.Exceptions.ApiError;
use Runtime.Exceptions.ItemNotFound;
use Runtime.Exceptions.RuntimeException;
use Runtime.Serializer.MapType;
use Runtime.Serializer.StringType;
use Runtime.Web.BaseApi;
use Runtime.Web.Annotations.ApiMethod;
use BayLang.Compiler.Project;
use BayLang.Constructor.Backend.ApiHook;
use BayLang.Constructor.Backend.Service.WidgetService;


class AssetsApi extends BaseApi
{
	/**
	 * Returns api name
	 */
	static string getApiName() => "baylang.constructor.assets";
	
	
	/**
	 * Upload file
	 */
	@ApiMethod{ "name": "upload" }
	async void uploadFile()
	{
		/* Get project */
		Project project = ApiHook::getProject();
		if (not project)
		{
			throw new ApiError(new ItemNotFound("project"))
		}
		
		/* Get file */
		File file = this.request.data.get("file");
		if (not file)
		{
			throw new ApiError(new RuntimeException("File not found"));
		}
		
		/* Filter data */
		MapType rules = new MapType{
			"path": new StringType(),
		};
		Map post_data = this.filter(this.request.data, rules);
		
		/* Get path */
		string assets_path = ApiHook::getAssetsPath(project);
		string folder_name = post_data.get("path");
		string folder_path = fs::join([assets_path, folder_name]);
		string file_upload_path = fs::join([folder_path, file.getName()]);
		
		/* Create folder */
		if (not await fs::exists(folder_path))
		{
			await fs::mkdir(folder_path);
		}
		
		/* Create file */
		string content = await fs::readFile(file.getPath());
		await fs::saveFile(file_upload_path, content);
		
		/* Result */
		this.success({
			"data":
			{
				"path": fs::join([folder_name, file.getName()]),
			}
		});
	}
	
	
	/**
	 * Remove file
	 */
	@ApiMethod{ "name": "remove" }
	async void removeFile()
	{
		/* Get project */
		Project project = ApiHook::getProject();
		if (not project)
		{
			throw new ApiError(new ItemNotFound("project"))
		}
		
		/* Filter data */
		MapType rules = new MapType{
			"file_path": new StringType(),
		};
		Map post_data = this.filter(this.request.data, rules);
		
		/* Get file path */
		string assets_path = ApiHook::getAssetsPath(project);
		string file_name = post_data.get("file_path");
		string file_path = fs::join([assets_path, file_name]);
		
		/* Check file path */
		if (not await fs::exists(file_path))
		{
			throw new ApiError(new ItemNotFound(file_name, "File"));
		}
		
		/* Remove file */
		await fs::unlink(file_path);
		
		/* Result */
		this.success({
			"data":
			{
				"file_path": file_path,
			}
		});
	}
	
	
	/**
	 * Returns files
	 */
	@ApiMethod{ "name": "files" }
	async void getFiles()
	{
		/* Get project */
		Project project = ApiHook::getProject();
		if (not project)
		{
			throw new ApiError(new ItemNotFound("project"))
		}
		
		/* Filter data */
		MapType rules = new MapType{
			"path": new StringType(),
		};
		Map post_data = this.filter(this.request.data, rules);
		
		/* Get assets path */
		string assets_path = ApiHook::getAssetsPath(project);
		string assets_url_path = ApiHook::getAssetsUrlPath(project);
		string folder_name = post_data.get("path");
		string folder_path = fs::join([assets_path, folder_name]);
		
		/* Check file path */
		if (rs::indexOf(folder_path, "/..") != -1)
		{
			throw new ApiError(new ItemNotFound(folder_name, "Folder"));
		}
		
		/* File not exists */
		if (not await fs::isFolder(folder_path))
		{
			throw new ApiError(new ItemNotFound(folder_name, "Folder"));
		}
		
		/* Get items */
		Collection items = [];
		Collection<string> files = await fs::listDir(folder_path);
		for (int i=0; i<files.count(); i++)
		{
			string item_name = files.get(i);
			string item_path = fs::join([folder_path, item_name]);
			string item_kind = "";
			
			if (await fs::isFile(item_path)) item_kind = "file";
			else if (await fs::isDir(item_path)) item_kind = "dir";
			
			Vector arr = [folder_name, item_name];
			arr = arr.filter(bool (string item) => item != "");
			items.push({
				"kind": item_kind,
				"file_name": item_name,
				"file_path": fs::join(arr),
				"url": assets_url_path ~ "/" ~ rs::join("/", arr),
			});
		}
		
		/* Result */
		this.success({
			"data":
			{
				"items": items,
				"path": folder_name,
			}
		});
	}
}