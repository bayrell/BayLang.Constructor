/*!
 *  BayLang Constructor
 */

namespace BayLang.Constructor.Backend.Api;

use Runtime.fs;
use Runtime.lib;
use Runtime.Exceptions.ApiError;
use Runtime.Exceptions.RuntimeException;
use Runtime.Web.Annotations.ApiMethod;
use Runtime.Widget.Crud.SaveApi;
use Runtime.Widget.Crud.Rules.CrudRule;
use Runtime.Widget.Crud.Rules.Required;
use BayLang.Constructor.Backend.ApiHook;
use BayLang.Helper.Project;


class ProjectSaveApi extends SaveApi
{
	/**
	 * Returns api name
	 */
	static string getApiName() => "baylang.constructor.project::save";
	
	
	/**
	 * Returns rules
	 */
	Vector<CrudRule> getRules() =>
	[
		new Required{"name": "id"},
		new Required{"name": "name"},
		new Required{"name": "template", "check_update": false},
	];
	
	
	/**
	 * Returns item fields
	 */
	Collection getItemFields() =>
	[
		"id",
		"name",
		"template",
	];
	
	
	/**
	 * Returns save fields
	 */
	Collection getSaveFields() =>
	[
		"id",
		"name",
		"template",
	];
	
	
	/**
	 * New item
	 */
	async void newItem() => new Project(null);
	
	
	/**
	 * Find item
	 */
	async void findItem(Dict pk)
	{
		string project_id = pk.get("id");
		return await Project::readProject(
			fs::join([@.env("constructor_path"), "projects", project_id])
		);
	}
	
	
	/**
	 * Reload nginx
	 */
	void reloadNginx()
	{
		string projects_path = fs::join([@.env("constructor_path"), "projects"]);
		Collection<string> files = fs::listDir(projects_path);
		Collection<string> content = [];
		for (int i=0; i<files.count(); i++)
		{
			string project_id = files.get(i);
			string project_dest = fs::join([projects_path, project_id])
			
			/* Add nginx */
			content.push("# Project " ~ project_id);
			content.push("location /project/" ~ project_id ~ "/iframe/assets/ {");
			content.push("\talias " ~ project_dest ~ "/src/public/assets/;");
			content.push("\tbreak;");
			content.push("}");
			content.push("location /project/" ~ project_id ~ "/iframe/ {");
			content.push("\tinclude fastcgi_params;");
			content.push("\troot " ~ project_dest ~ "/src/public;");
			content.push("\tfastcgi_param CONSTRUCTOR true;");
			content.push("\tfastcgi_param SCRIPT_FILENAME $document_root/index.php;");
			content.push("\tfastcgi_param HTTP_X_FORWARDED_PREFIX " ~
				"$http_x_forwarded_prefix/project/" ~ project_id ~ "/iframe;");
			content.push("\tbreak;");
			content.push("}");
			
			if (i != files.count() - 1)
			{
				content.push("");
			}
		}
		
		/* Save nginx */
		string nginx_path = fs::join([@.env("constructor_path"), "nginx/nginx.conf"]);
		string nginx_content = rs::join("\n", content);
		await fs::saveFile(nginx_path, nginx_content);
		
		/* Reload nginx */
		#ifcode PHP then
		shell_exec("sudo /usr/sbin/nginx -s reload");
		#endif
	}
	
	
	/**
	 * Create nginx project file
	 */
	async void createNginxProjectFile(Project project)
	{
		/* Get domains */
		Collection<string> domains = [];
		if (project.info.has("domains"))
		{
			domains = project.info.get("domains");
		}
		
		/* Filter domains */
		domains = domains.filter(lib::equalNot(""));
		domains = domains.removeDuplicates();
		
		/* Get file name */
		string projects_path = fs::join([@.env("constructor_path"), "projects"]);
		string nginx_file_name = fs::join([
			@.env("constructor_path"), "nginx/domains",
			project.getID() ~ ".conf"
		]);
		
		/* Create file */
		Collection<string> content = [];
		if (domains.count() > 0)
		{
			string project_id = project.getID();
			string project_dest = fs::join([projects_path, project_id])
			string server_name = rs::join(" ", domains);
			
			/* Add nginx */
			content.push("server {");
			content.push("\tlisten 80;");
			content.push("\tserver_name " ~ server_name ~ ";");
			content.push("\troot " ~ project_dest ~ "/src/public;");
			content.push("\tindex index.php index.html;");
			content.push("\tautoindex off;");
			content.push("\tlocation / {");
			content.push("\t\tif (-f $request_filename) {");
			content.push("\t\t\tbreak;");
			content.push("\t\t}");
			content.push("\t\trewrite ^/. /index.php last;");
			content.push("\t}");
			content.push("\tlocation /index.php {");
			content.push("\t\tinclude fastcgi_params;");
			content.push("\t\tbreak;");
			content.push("\t}");
			content.push("}");
			
			/* Save nginx */
			string nginx_content = rs::join("\n", content);
			await fs::saveFile(nginx_file_name, nginx_content);
		}
		else
		{
			if (await fs::exists(nginx_file_name))
			{
				await fs::unlink(nginx_file_name);
			}
		}
	}
	
	
	/**
	 * Create project
	 */
	async void createProject()
	{
		string project_id = this.data.get("id");
		string project_template = this.data.get("template");
		
		/* Check project */
		Project project = await Project::readProject(
			fs::join([@.env("constructor_path"), "projects", project_id])
		);
		if (project)
		{
			throw new ApiError(new RuntimeException("Project already exists"));
		}
		
		/* Get project path */
		string project_archive = fs::join([@.base_path, "examples", "clear.zip"]);
		string project_dest = fs::join([@.env("constructor_path"), "projects", project_id])
		
		/* Create project folder */
		if (not await fs::exists(project_dest))
		{
			await fs::mkdir(project_dest);
		}
		
		/* Copy project */
		#ifcode PHP then
		shell_exec("unzip $project_archive -d $project_dest");
		#endif
		
		/* Read new project */
		this.item = await Project::readProject(
			fs::join([@.env("constructor_path"), "projects", project_id])
		);
	}
	
	
	/**
	 * Reload cache
	 */
	@ApiMethod
	async void reloadCache()
	{
		/* Load data */
		await this.loadItem(false);
		
		/* Project path */
		string project_path = this.item.getPath();
		string cache_path = fs::join([project_path, ".cache"]);
		
		/* Remove cache */
		#ifcode PHP then
		shell_exec("rm -rf $cache_path");
		#endif
		
		/* Load project */
		await this.item.load();
		
		/* Success */
		this.success();
	}
	
	
	/**
	 * Action item
	 */
	@ApiMethod
	async void actionItem()
	{
		/* Load data */
		await this.loadItem(false);
		
		/* Get domains */
		Collection<string> domains = [];
		if (this.item.info.has("domains"))
		{
			domains = this.item.info.get("domains");
		}
		
		/* Success */
		this.success({
			"data":
			{
				"item":
				{
					"id": this.item.getID(),
					"name": this.item.getName(),
					"description": this.item.getDescription(),
					"domains": domains,
				},
			},
		});
	}
	
	
	/**
	 * Action save
	 */
	@ApiMethod
	async void actionSave()
	{
		/* Load data */
		await this.loadItem();
		await this.loadData();
		
		/* Create project */
		if (this.pk == null)
		{
			await this.createProject();
		}
		
		/* Set project info */
		if (this.data.has("name"))
		{
			this.item.setName(this.data.get("name"));
		}
		if (this.data.has("description"))
		{
			this.item.setDescription(this.data.get("description"));
		}
		if (this.data.has("domains"))
		{
			this.item.info.set("domains", this.data.get("domains"));
		}
		
		/* Save project */
		await this.item.saveInfo();
		
		/* Create nginx file */
		await this.createNginxProjectFile(this.item);
		
		/* Reload nginx */
		await this.reloadNginx();
		
		/* Success */
		this.success();
	}
	
	
	/**
	 * Action delete
	 */
	@ApiMethod
	async void actionDelete()
	{
		/* Load data */
		await this.loadItem();
		
		/* Remove nginx file */
		string nginx_file_name = fs::join([
			@.env("constructor_path"), "nginx/domains",
			this.item.getName() ~ ".conf"
		]);
		if (await fs::exists(nginx_file_name))
		{
			await fs::unlink(nginx_file_name);
		}
		
		/* Project path */
		string project_dest = this.item.getPath();
		
		/* Remove project */
		#ifcode PHP then
		shell_exec("rm -rf $project_dest");
		#endif
		
		/* Reload nginx */
		await this.reloadNginx();
		
		/* Success */
		this.success();
	}
}