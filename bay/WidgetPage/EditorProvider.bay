/*!
 *  BayLang Constructor
 */

namespace BayLang.Constructor.WidgetPage;

use Runtime.lib;
use Runtime.BaseObject;
use Runtime.BaseProvider;
use Runtime.Web.BaseLayoutModel;
use BayLang.Constructor.WidgetPage.WidgetManagerAnnotation;
use BayLang.Constructor.WidgetPage.WidgetSettingsInterface;


class EditorProvider extends BaseProvider
{
	Collection<Dict> groups = [];
	Collection<WidgetManagerAnnotation> managers = [];
	Collection<WidgetSettingsInterface> widgets = [];
	Dict<WidgetSettingsInterface> settings = {};
	
	
	/**
	 * Add group
	 */
	void addGroup(Dict group)
	{
		string group_name = group.get("name");
		Dict item = this.groups.findItem(lib::equalAttr("name", group_name));
		if (item != null) return;
		this.groups.push(group);
	}
	
	
	/**
	 * Init provider
	 */
	async void init()
	{
		/* Get widgets managers */
		Vector<WidgetManagerAnnotation> managers = @.getEntities(classof WidgetManagerAnnotation);
		for (int i=0; i<managers.count(); i++)
		{
			WidgetManagerAnnotation annotation = managers.get(i);
			this.managers.push(annotation.factory());
		}
		
		for (int i=0; i<this.managers.count(); i++)
		{
			WidgetManagerAnnotation manager = this.managers.get(i);
			
			/* Get groups settings */
			Dict groups = manager.getGroupSettings();
			Collection group_names = groups.keys();
			for (int i=0; i<group_names.count(); i++)
			{
				string group_name = group_names.get(i);
				Dict group = groups.get(group_name);
				group.set("name", group_name);
				this.addGroup(group);
			}
			
			/* Get widgets settings */
			Collection<string> widgets = manager.getWidgetSettings();
			for (int j=0; j<widgets.count(); j++)
			{
				WidgetSettingsInterface widget_settings = widgets.get(j);
				
				/* Add widget */
				this.widgets.push(widget_settings);
				
				/* Add widget by name */
				var component_name = widget_settings.getComponentName();
				if (component_name)
				{
					this.settings.set(component_name, widget_settings);
				}
			}
		}
		
		/* Sort groups */
		this.groups = this.groups.sort(lib::sortAttr("priority", "asc"));
	}
	
	
	/**
	 * Returns groups list
	 */
	Collection getGroups() => this.groups.slice();
	
	
	/**
	 * Returns widgets list
	 */
	Collection<WidgetSettingsInterface> getWidgets() => this.widgets.slice();
	
	
	/**
	 * Find settings by class name
	 */
	Collection<WidgetSettingsInterface> findClassName(string class_name) =>
		this.widgets.findItem(
			bool (WidgetSettingsInterface item) use (class_name) =>
				item::getClassName() == class_name
		)
	;
	
	
	/**
	 * Get widget by component name
	 */
	WidgetSettingsInterface getWidgetSettings(string component_name) =>
		this.settings.get(component_name)
	;
}