/*!
 *  BayLang Technology
 *
 *  (c) Copyright 2016-2026 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace BayLang.Constructor.WidgetPage;

use Runtime.BaseModel;
use Runtime.Serializer.ObjectType;
use Runtime.Serializer.StringType;
use Runtime.Widget.ContextMenu.ContextMenuMessage;
use Runtime.Widget.ContextMenu.ContextMenuModel;
use BayLang.Constructor.Frontend.Editor.EditorPageModel;
use BayLang.Constructor.WidgetPage.WidgetPage;


class WidgetPageModel extends BaseModel
{
	string component = classof WidgetPage;
	string page_model = "";
	EditorPageModel editor = null;
	ContextMenuModel contextmenu = null;
	var selected_elem = null;
	string selected_style = "";
	
	
	/**
	 * Serialize object
	 */
	static void serialize(ObjectType rules)
	{
		parent(rules);
		rules.addType("page_model", new StringType());
	}
	
	
	/**
	 * Init params
	 */
	void initParams(Map params)
	{
		parent(params);
		
		if (params.has("page_model")) this.page_model = params.get("page_model");
	}
	
	
	/**
	 * Init widget
	 */
	void initWidget(Map params)
	{
		parent(params);
		
		/* Contextmenu */
		this.contextmenu = this.createWidget(classof ContextMenuModel, {
			"items": [
				{
					"label": "Settings",
					"key": "settings",
				}
			]
		});
		this.contextmenu.addEventListener("clickItem", method this.onContextMenuClick);
		
		/* Editor */
		this.editor = this.createWidget(classof EditorPageModel, {
			"page_model": this.page_model,
			"page_widget": this,
		});
	}
	
	
	/**
	 * Returns page model
	 */
	BaseModel getPageModel()
	{
		return this.layout.pages.get(this.page_model);
	}
	
	
	/**
	 * Context menu click
	 */
	async void onContextMenuClick(ContextMenuMessage message)
	{
		this.contextmenu.hide();
		
		string key = message.item.get("key");
		if (key == "settings")
		{
			this.editor.showSettings();
		}
	}
	
	
	/**
	 * Save
	 */
	async void save()
	{
		await this.editor.save();
	}
	
	
	/**
	 * Selected element
	 */
	void selectElement(var elem)
	{
		this.selected_elem = elem;
		this.updateSelectedBox();
	}
	
	
	/**
	 * Update selected box
	 */
	void updateSelectedBox()
	{
		if (not this.selected_elem)
		{
			this.selected_style = "";
			return;
		}
		Vector arr = [];
		arr.push("left: " ~ this.selected_elem.offsetLeft);
		arr.push("top: " ~ this.selected_elem.offsetTop);
		arr.push("width: " ~ this.selected_elem.offsetWidth);
		arr.push("height: " ~ this.selected_elem.offsetHeight);
		this.selected_style = rs::join(";", arr);
	}
}